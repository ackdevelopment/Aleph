module gen.GenVisitor;

import std.file;
import stdio = std.stdio;
import std.range;
import std.conv;

import syntax.tree.visitors.ASTVisitor;
import symbol.SymbolTable;
import gen.GenUtils;
public import gen.OutputBuilder;

auto generate(SymbolTable table, ASTNode node, OutputStream outp)
{
    return new GenVisitor(table, new OutputBuilder(outp)).apply(node);
}

final class GenVisitor : ASTVisitor {
private:
    OutputBuilder *ob;
    SymbolTable table;

    alias ob this;

    void visitParameters(ProcDeclNode node)
    {
        foreach(i, x; node.parameters){
            this.ob.untabbed({
                this.ob.printf("%s %s", x.type.toCtype, x.name);
                if(i < node.parameters.length - 1){
                    this.ob.printf(", ");
                }
            });
        }
    }
public:
    this(SymbolTable table, ref OutputBuilder output)
    {
        this(table, &output);
    }

    this(SymbolTable table, OutputBuilder *output)
    {
        this.table = table;
        this.ob = output;
    }

    OutputBuilder *apply(ASTNode node)
    {
        this.printfln("/* Generated by the Aleph Compiler v0.0.1 */");
        this.printfln("/* Author: Kaden Thomas */");
        this.printfln("");
        this.dispatch(node);
        return this.ob;
    }
override:
    void visit(ProgramNode node)
    {
        foreach(x; node.children){
            this.dispatch(x);
        }
    }

    void visit(ReturnNode node)
    {
        printf("return");
        untabbed({
            if(node.value){
                this.printf(" ");
                this.dispatch(node.value);
            }
        });
    }

    void visit(ProcDeclNode node)
    {
        import util.match;
        import std.range;
        import std.algorithm;

        this.printf(
            "%s %s(",
            node.returnType.toCtype,
            node.name
        );
        this.visitParameters(node);
        this.printfln(")");

        this.dispatch(node.bodyNode);
        printfln("");
    }

    void visit(CallNode node)
    {
        dispatch(node.toCall);
        untabbed({
            this.printf("(");
            foreach(i, x; node.arguments){
                this.dispatch(x);
                if(i < node.arguments.length - 1){
                    this.printf(", ");
                }
            }
            this.printf(")");
        });
    }

    void visit(BlockNode node)
    {
        block({
            foreach(x; node.children){
                this.dispatch(x);
                this.untabbed({
                    this.printfln(";");
                });
            }
        });
    }

    void visit(IdentifierNode node)
    {
        printf(node.name);
    }

    void visit(IntegerNode node)
    {
        printf("%d", node.value);
    }

    void visit(CharNode node)
    {
        printf("%c", node.value);
    }
};
